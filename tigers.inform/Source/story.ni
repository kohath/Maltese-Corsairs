"The Maltese Corsairs"

Part 1 - Setup

Include Locksmith by Emily Short.  Use American dialect, the serial comma, and no scoring.  Release along with a website and a source text.

Chapter 1 - Play Rules

Section 1 - Directions

The starboard is a direction. The starboard has opposite port. Understand "s" as starboard when the location is nautical.
The port is a direction. The port has opposite starboard. Understand "p" as port when the location is nautical.
The fore is a direction. The fore has opposite aft. Understand "f" as fore when the location is nautical.  Understand "forward" as fore.
The aft is a direction. The aft has opposite fore. Understand "a" as aft when the location is nautical.

Does the player mean going a nautical direction when the location is nautical: it is very likely.
Index map with fore mapped as east. Index map with aft mapped as west. Index map with port mapped as north. Index map with starboard mapped as south.

A room can be nautical or earthbound. A room is usually nautical. A direction can be nautical or earthbound. A direction is usually not nautical. A direction can be exclusively nautical. A direction is usually not exclusively nautical.  Starboard, port, fore, aft, up, down, the inside and the outside are nautical.  Starboard, port, fore, and aft are exclusively nautical.

Before going an exclusively nautical direction when the location is not nautical, say "[bracket]We ain't on board, amico, but you can tell which way north is, and you can go [list of viable directions] from here.[close bracket][line break]" instead.

Before going an earthbound direction when the location is nautical:
	let the thataway be "[noun]" in sentence case;
	say "[bracket][thataway]?  It's [list of exclusively nautical directions] when we're aboard ship, amico, and it looks like you can go [list of viable directions] from here.[close bracket][line break]" instead.

Before going nowhere, say "[bracket]You can't go that way, amico.  It looks like you can only go [list of viable directions].[close bracket][line break]" instead.

Definition: a direction (called thataway) is viable if the room thataway from the location is a room.

Section 2 - Time

To say (relevant time - a time) as full ship's bell time:
	let M be the minutes part of relevant time;
	say "[relevant time as ship's bell time] [the remainder after dividing M by 30] min[s]"
	
To say (relevant time - a time) as ship's bell time:
	let M be the minutes part of relevant time;
	let B be (the remainder after dividing the hours part of relevant time by 4) multiplied by 2;
	if B is 0 then change B to 8;
	say "[if M is greater than 29][the remainder after dividing B plus 1 by 8][otherwise][B][end if] bell[s]"

When play begins: 
	change the right hand status line to "[time of day as ship's bell time]".
	
[Start the game at a random time of day?]

Chapter 2 - Setting

The Hold is a room.  "[if unvisited]On some occasions, the hold is an empty cavern full of not much beside the sound of waves.  Not this week, though.  [end if]Crates and crates--of cinnamon, of all things--line the bulkheads, leaving only a narrow path aft to the brig from the stair leading up to the crew quarters."

The Brig is aft of the Hold. "It's a small room, with a barred cell that takes up half of it.  The walls are a dingy white and the small lamp at the guard's desk doesn't do much to enliven the place. [if unvisited]  We don't normally deal in prisoners, so most of the time brig duty is just a chance to sneak in an extra shift of sleep.[end if]"

Crew Quarters are up from the Hold.  "A row of hammocks line the berth deck along the starboard side, which curves to create a moderately open space.  A curtained alcove off the opposite side secludes the cannon master's bed from casual view.[if unvisited] You still haven't been able to figure out why the captain won't set up cabins for everyone. [end if][paragraph break]If you need to hit the head, the door is on the fore wall; the mess is aft, and stairs lead down to the hold and up to the Cannons."

The Cannons are up from Crew Quarters.  "The gun deck is long with a low ceiling, lit only by dim shafts of sunlight falling on rows of cannons aimed outwards.[paragraph break]Stairs lead down to the crew quarters and up to the deck."

The Head is fore from Crew Quarters.  "A place for doing one's business.  The less said about the condition of this room, the better.  The exit is aft."

The Mess is aft from Crew Quarters.  "A pair of long tables are the main feature of the mess. [if unvisited]You can probably count on one hand the number of times the whole crew has sat down to eat together, though. [end if]Rock generally leaves a plate or pot of something out for folk, and today is no exception."

The Galley is aft from the Mess.  "The ship's kitchen is not very large, and the presence of the cook generally leaves it much more cramped.  Cabinets line the upper and lower walls, and a small bench sits by the entrance, fore."

The Deck is up from the Cannons. "The sea stretches out in every direction... but you're standing on the deck of the Joli, which feels cramped by comparison. [if unvisited](You never did get the hang of how much smaller life aboard ship is.) [end if]The entrance to the captain's hall is aft, and the deck extends in the opposite direction to the fo'c'sle.  You could also climb up to the crows['] nest, or go down to the Cannons."

The Forecastle is fore from the Deck.  "You could probably sit here and watch the ship plow the Mediterranean all day, but you likely have other things to do."

The Captain's Hall is aft from the Deck and inside from the Deck.  "The captain generally keeps all his equipment here, more likely than not stacked at random on the grand table at the center of the room.  The walls are also bedecked with miscellany finds from the ship's travels, including a shelf with a radio that won't be useful for another hundred years or so.  The captain's quarters are to starboard and the first mate's to port."

The Captain's Quarters are starboard from the Captain's Hall.  "In contrast to his hall, Bluebeard's quarters are spartan and tidy.  A hammock hangs by a porthole starboard, and the chest with the captain's effects sits beside it."  The Captain's Hall is outside from the Captain's Quarters.

The First Mate's Quarters are port from the Captain's Hall.  "Frostbite's cabin is full of piles of crates, chests, and boxes--so much so it's hard to see where he sleeps[if unvisited]...if he sleeps at all[end if]." The Captain's Hall is outside from the First Mate's Quarters. 

Inside from the Captain's Hall is nowhere.  

The Lifeboat is starboard from the Deck.  "It's one of those little boats one hopes one never has to find oneself needing... and not just because of how cramped it'd be with most of the crew crammed into it.  On a sunny day off, though, it's just another place to take in some shut-eye."

The Crows' Nest is up from the Deck.  "The view from the crow's nest is impressive--the blue-green sea in every direction, and the deck below mostly obscured by the rigging.   Unfortunately it's a tiny space in and of itself, which makes you despise every second duty keeps you up here."


Chapter 3 - Actors

The player is in the Hold.  The description of the player is "You're Jean Waring, a blue tiger.  Folk said you'd never be man enough to be a pirate, but here you are."

Bluebeard is a man in the Captain's Hall.  "Bluebeard stands at his table, examining some maps."  The description of Bluebeard is "The ship's captain is a gruff burly old dark-furred tiger whose eponymous beard, short but thick, is actually closer to black." [Adjectives much?]  Bluebeard wears a black tricorn, a leather harness,  an eyepatch, black boots, shorts, and a dark vest.  Bluebeard carries a sword and a pistol.

Frostbite is a man in the Forecastle.  "Frostbite is here, leaning against the railing and gazing out at the water."  The description of Frostbite is "The first mate is lean but strong, with ice-blue fur.  He carries himself like a gentleman, but he's certainly dangerous: they say he was just called Frost till he bit another man's leg off in a fight.  Rumor says he's part wolf, but he's always looked all tiger to you."  

Roquefort is a man in the Galley.  "Roquefort the cook is here, chopping up [one of]an onion[or]a pile of carrots[or]a slab of meat[at random] with a large cleaver." The description of Roquefort is "The cook is a good three hundred fifty pounds, and it's easy to see why--he never does seem to stop eating.  Rock makes good food, all right, but he hasn't got the most pleasant personality; he'll throw you out of the mess for the slightest offense against his unwritten code of conduct."

Fiordaliso is a man in the Crew Quarters.  "Fiordaliso is asleep in his hammock."  The description of Fiordaliso is "The night lookout is a small, scrawny fellow with scraggly whiskers.  His fur is a dark blue-grey and he usually dresses all in black.  There are three piercings in his right ear.  You may not believe the stories about the first mate being part wolf, but every time you look at Fiord you can't help but think the tiger may be mostly rat."

I単igo is a man in the Crows' Nest.  "I単igo sits here, keeping watch."  The description of I単igo is "The day lookout is probably the one Corsair you find most like yourself: sort of an everyday tiger, the sort you wouldn't expect to find on a pirate ship.  You've taken to it a lot better than he has so far though." Understand "Inigo" or "Innigo" as I単igo. 

Skye is a man in the Cannons.  "Skye leans against one of the cannons, looking out to sea."  The description of Skye is "The ship's second mate is not a tiger at all, in fact, but a rather stocky lion, somewhat on the older side of middle age.  To his credit, his fur, where it is not beginning to streak with gray, is a rather bright cerulean hue."

Zac is a man in the Mess.  "Zac is here, scrubbing down a table."  The description of Zac is "The cabin boy is an older teenager, awkward in his movements, but he's a sharp young tiger, and strong enough to handle the odd jobs he gets thrown every day." Understand "Zak" or "Zack" or "Zach" as Zac.

Suede is a man in the Brig.  "Suede sits at the guard's desk, playing solitaire."  The description of Suede is "The day guard is a lean and bored-looking fellow, a dark blue tiger with a habit of wearing a short-brimmed visor to shade his eyes, even in the dimness below decks."

Laszlo is a man in the Crew Quarters.  "Laszlo is snoring loudly, asleep in his hammock."  The description of Laszlo is "The night guard is a very stout tiger, almost as well-built as the captain, and certainly bigger.  But even though he could certainly take down anyone aboard in a fight, you've never actually seen any aggressive behaviour from him."

[Read through chapter 3.23 of doc.]

[Need verb for 'who is'/'what is' (wi).]

[Hunt down Lingua Franca words.]

[deck of cards]
[ship's bell]
